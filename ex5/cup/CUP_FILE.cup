/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import ast.*;

parser code
{:
	public Lexer lexer;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print(":");
		System.out.print(lexer.getTokenStartPosition());
		System.out.print("] ");
		System.out.print("\n");
	}
	public void syntax_error(Symbol cur_token)
	{
		report_error("Syntax error", cur_token);
		throw new RuntimeException("Syntax error at line " + lexer.getLine());
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	//System.out.print(s.sym);
	// System.out.print("[");
	// System.out.print(lexer.getLine());
	// System.out.print(":");
	// System.out.print(lexer.getTokenStartPosition());
	// System.out.print("] ");
	// System.out.print(TokenNames.terminalNames[s.sym]);
	// if (s.value != null)
	// {
	// 	System.out.print("( ");
	// 	System.out.print(s.value);
	// 	System.out.print(" )");
	// }
	// System.out.print("\n");
	return s;
:};

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal ELSE;
terminal NEW;
terminal EXTENDS;
terminal EQ;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;
terminal TYPE_INT;
terminal TYPE_STRING;
terminal TYPE_VOID;
terminal NIL;
terminal COMMA;
terminal ARRAY;
terminal CLASS;
terminal RETURN;
terminal LT;
terminal GT;
terminal Integer INT;
terminal String ID;
terminal String STRING;

/*****************/
/* NON TERMINALS */
/*****************/

non terminal AstDecList 		program;
non terminal AstDec 			dec;
non terminal AstDecClass		classDec;
non terminal AstDecVar 			varDec;
non terminal AstDecFunc			funcDec;
non terminal AstParam			param;
non terminal AstParametersList	parametersList;
non terminal AstFieldList		cFieldList;
non terminal AstField			cField;

non terminal AstExp 			exp;
non terminal AstExpNew			newExp;
non terminal AstExpList			expList;
non terminal AstExpCall			callExp;
non terminal AstVar 			var;
non terminal AstStmt 			stmt;
non terminal AstStmtList 		stmtList;
non terminal AstType 			type;
non terminal AstArrayTypedef 	arrayTypedef;

/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence right ASSIGN;
precedence left EQ;
precedence left LT, GT;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left LBRACK;
precedence left LPAREN;
precedence left DOT;

/***********************/
/* START WITH: program */
/***********************/
start with program;

/********************/
/* DERIVATION RULES */
/********************/

/* program is a list of declarations */
program		::=		dec:d program:p			{: RESULT = new AstDecList(d, p, dleft+1); :}
					| dec:d					{: RESULT = new AstDecList(d, null, dleft+1); :}
					;

/* declarations */
dec			::= 	varDec:v 				{: RESULT = new AstDec(v, vleft+1);		:}
					| funcDec:f				{: RESULT = new AstDec(f, fleft+1);		:}
					| classDec:c 			{: RESULT = new AstDec(c, cleft+1);		:}
					| arrayTypedef:a 		{: RESULT = new AstDec(a, aleft+1);		:}
					;

/* types */
type		::=		TYPE_INT:t				{: RESULT = new AstType("int", tleft+1);     	:}
                	| TYPE_STRING:t			{: RESULT = new AstType("string", tleft+1);   	:}
                	| TYPE_VOID:t			{: RESULT = new AstType("void", tleft+1);     	:}
                	| ID:name				{: RESULT = new AstType(name, nameleft+1);       	:}
                	;

/* variable declaration */
varDec		::=		type:t ID:i SEMICOLON 					{: RESULT = new AstDecVar(i, t, tleft+1);		:}
					| type:t ID:i ASSIGN exp:e SEMICOLON	{: RESULT = new AstDecVar(i, t, e, tleft+1);		:}
					| type:t ID:i ASSIGN newExp:e SEMICOLON {: RESULT = new AstDecVar(i, t, e, tleft+1);	:}
					;

/* function declaration */
funcDec		::=		type:t ID:i LPAREN RPAREN LBRACE stmtList:s RBRACE 							{: RESULT = new AstDecFunc(t, i, null, s, tleft+1);	:}
					| type:t ID:i LPAREN parametersList:p RPAREN LBRACE stmtList:s RBRACE 		{: RESULT = new AstDecFunc(t, i, p, s, tleft+1);		:}
					;

/* parameter list */
parametersList  ::=	type:t ID:i COMMA parametersList:tail 	{: RESULT = new AstParametersList(new AstParam(t, i, tleft+1), tail, tleft+1);	:}
					| type:t ID:i 							{: RESULT = new AstParametersList(new AstParam(t, i, tleft+1), null, tleft+1);	:}
					;

/* class declaration */
classDec	::=		CLASS:c ID:name LBRACE cFieldList:fields RBRACE 					{: RESULT = new AstDecClass(name, null, fields, cleft+1);	:}
					| CLASS:c ID:name EXTENDS ID:parent LBRACE cFieldList:fields RBRACE 	{: RESULT = new AstDecClass(name, parent, fields, cleft+1);	:}
					;

/* class field list */
cFieldList	::=		cField:f cFieldList:l		{: RESULT = new AstFieldList(f, l, fleft+1);		:}
					| cField:f					{: RESULT = new AstFieldList(f, null, fleft+1);	:}
					;

/* class field */
cField		::=		varDec:v					{: RESULT = new AstField(v, vleft+1);	:}
					| funcDec:f					{: RESULT = new AstField(f, fleft+1);	:}
					;

/* array typedef */
arrayTypedef ::=	ARRAY:a ID:name EQ type:t LBRACK RBRACK SEMICOLON {: RESULT = new AstArrayTypedef(name, t, aleft+1);	:}
					;

/* statement list */
stmtList	::=		stmt:s	stmtList:l		{: RESULT = new AstStmtList(s,l, sleft+1);    :}
					| stmt:s				{: RESULT = new AstStmtList(s,null, sleft+1); :}
					;

/* variable */
var			::=		ID:name								{: RESULT = new AstVarSimple(name, nameleft+1);       :}
					| var:v DOT ID:fieldName			{: RESULT = new AstVarField(v,fieldName, vleft+1); :}
					| var:v LBRACK exp:e RBRACK			{: RESULT = new AstVarSubscript(v,e, vleft+1);     :}
					;

/* statements */
stmt		::=		varDec:v												{: RESULT = new AstStmtVarDec(v, vleft+1); 			:}
					| var:v ASSIGN exp:e SEMICOLON							{: RESULT = new AstStmtAssign(v,e, vleft+1); 		:}
					| var:v ASSIGN newExp:e SEMICOLON						{: RESULT = new AstStmtAssignNew(v,e, vleft+1); 		:}
					| RETURN:r SEMICOLON									{: RESULT = new AstStmtReturn(null, rleft+1); 		:}
					| RETURN:r exp:e SEMICOLON								{: RESULT = new AstStmtReturn(e, rleft+1); 			:}
					| IF:i LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AstStmtIf(cond,body, ileft+1); 		:}
					| IF:i LPAREN exp:cond RPAREN LBRACE stmtList:ifBody RBRACE ELSE LBRACE stmtList:elseBody RBRACE
																			{: RESULT = new AstStmtIfElse(cond,ifBody,elseBody, ileft+1); :}
					| WHILE:w LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AstStmtWhile(cond,body, wleft+1); 	:}
					| callExp:c SEMICOLON									{: RESULT = new AstStmtCallExp(c, cleft+1); 			:}
					;

/* expressions */
exp			::=		var:v								{: RESULT = new AstExpVar(v, vleft+1);          		:}
					| LPAREN exp:e RPAREN				{: RESULT = e;          					:}
					| exp:e1 PLUS  exp:e2				{: RESULT = new AstExpBinop(e1, e2, ast.BinOp.PLUS, e1left+1);		:}
					| exp:e1 MINUS exp:e2				{: RESULT = new AstExpBinop(e1, e2, ast.BinOp.MINUS, e1left+1);		:}
					| exp:e1 TIMES exp:e2				{: RESULT = new AstExpBinop(e1, e2, ast.BinOp.TIMES, e1left+1);		:}
					| exp:e1 DIVIDE exp:e2				{: RESULT = new AstExpBinop(e1, e2, ast.BinOp.DIVIDE, e1left+1);		:}
					| exp:e1 LT exp:e2					{: RESULT = new AstExpBinop(e1, e2, ast.BinOp.LT, e1left+1);		:}
					| exp:e1 GT exp:e2					{: RESULT = new AstExpBinop(e1, e2, ast.BinOp.GT, e1left+1);		:}
					| exp:e1 EQ exp:e2					{: RESULT = new AstExpBinop(e1, e2, ast.BinOp.EQ, e1left+1);		:}
					| callExp:c							{: RESULT = c;          					:}
					| INT:i								{: RESULT = new AstExpInt(i, ileft+1);          		:}
					| MINUS:m INT:i						{: RESULT = new AstExpInt(-i, mleft+1);          	:}
					| NIL:n								{: RESULT = new AstExpNil(nleft+1);          		:}
					| STRING:s							{: RESULT = new AstExpString(s, sleft+1);          	:}
					;

/* new expression */
newExp		::=		NEW:n type:t							{: RESULT = new AstExpNew(t, nleft+1);			:}
					| NEW:n type:t LBRACK exp:e RBRACK	{: RESULT = new AstExpNew(t, e, nleft+1);		:}
					;

/* call expression */
callExp		::=		ID:name LPAREN RPAREN						{: RESULT = new AstExpCall(name, null, nameleft+1);			:}
					| ID:name LPAREN expList:params RPAREN		{: RESULT = new AstExpCall(name, params, nameleft+1);		:}
					| var:v DOT ID:name LPAREN RPAREN			{: RESULT = new AstExpCall(v, name, null, vleft+1);		:}
					| var:v DOT ID:name LPAREN expList:params RPAREN	{: RESULT = new AstExpCall(v, name, params, vleft+1);	:}
					;

/* expression list */
expList		::=		exp:e COMMA expList:tail		{: RESULT = new AstExpList(e, tail, eleft+1);	:}
					| exp:e							{: RESULT = new AstExpList(e, null, eleft+1);	:}
					;